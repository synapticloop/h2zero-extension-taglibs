buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
		maven { url "https://plugins.gradle.org/" }
	}

	dependencies {
		classpath 'com.synapticloop:h2zero:' + findProperty("h2zero.version")
		classpath 'com.synapticloop:h2zero-extension-taglibs:' + findProperty("h2zero.version")
	}
}

plugins {
	id "java"
	id "eclipse"
	id "maven-publish"

	id "com.github.ben-manes.versions" version "0.51.0"
	id "synapticloop.copyrightr" version "1.3.1"
}

version = findProperty("h2zero.version")
group = 'com.synapticloop'
description = """Taglib extension for h2zero"""

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
	withSourcesJar()
	withJavadocJar()
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	implementation 'com.synapticloop:h2zero:6.0.0'
	implementation "com.synapticloop:templar:2.0.0"
	implementation "org.json:json:20240303"
	implementation "com.mchange:c3p0:0.10.0"
	implementation "commons-validator:commons-validator:1.8.0"
	implementation "org.slf4j:slf4j-api:1.7.26"

	implementation 'jakarta.servlet.jsp:jakarta.servlet.jsp-api:3.1.0'
	implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'

	testImplementation 'junit:junit:4.13.2'
	testImplementation 'org.mockito:mockito-all:1.10.19'
	testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
	testImplementation 'mysql:mysql-connector-java:8.0.33'
	testImplementation 'org.xerial:sqlite-jdbc:3.30.1'
	testImplementation 'commons-io:commons-io:2.16.0'
	testImplementation 'com.mashape.unirest:unirest-java:1.4.9'
}

configurations.all {
	resolutionStrategy {
		failOnVersionConflict()
		force 'commons-logging:commons-logging:1.3.0',
					'org.json:json:20240303',
					'junit:junit:4.13.2',
					'org.apache.httpcomponents:httpclient:4.5.2'
	}
}

test {
	include '**/*Test.class'
	maxParallelForks = 1
}



publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId "com.synapticloop"
			artifactId 'h2zero-extension-taglibs'
			version version

			from components.java
		}
	}
}

copyrightr {
	dryRun = false
	copyrightHolder = "synapticloop"
	includes = [
			"src/**/*.java",
			"src/**/*.groovy"
	]

	excludes = [
			"src/test/java/**/*.java",
			"**/*.class"
	]

	onlyReplaceFirst = true

	yearSeparator = " - "

	failOnMissing = false
}
